if (currentBuild.getBuildCauses().toString().contains('BranchIndexingCause')) {
  print "INFO: Build skipped due to trigger being Branch Indexing"
  githubNotify context: 'Jenkins', description: 'Build skipped due to branch indexing', status: 'SUCCESS'
  currentBuild.result = 'ABORTED' // optional, gives a better hint to the user that it's been skipped, rather than the default which shows it's successful
  return
}
pipeline {
    options {
        copyArtifactPermission('Annotare-web-app-release-wwwdev, Annotare-web-app-release-prod');
        buildDiscarder(logRotator(
          artifactDaysToKeepStr: '180',
          artifactNumToKeepStr: '20',
          daysToKeepStr: '180',
          numToKeepStr: '20',
        ));
        parallelsAlwaysFailFast();
    }
    // Do not start the main build pod up-front. We will first ensure
    // the required PVCs exist, then run the build in a second stage
    // that uses the pod template mounting those PVCs.
    agent none
    stages {
        stage('Prepare PVCs') {
            agent {
                kubernetes {
                    cloud 'atlas-admin-2'
                    // Minimal pod just to run kubectl; no PVC mounts here
                    yaml '''
apiVersion: v1
kind: Pod
spec:
  restartPolicy: Never
  containers:
    - name: kubectl
      image: bitnami/kubectl:latest
      imagePullPolicy: IfNotPresent
      command: ["sh", "-c", "sleep 365d"]
      tty: true
      stdin: true
'''
                    defaultContainer 'kubectl'
                }
            }
            steps {
                sh '''
                  set -euo pipefail
                  echo "Ensuring required PVCs exist (annotare-m2-cache, annotare-mysql-cache) before scheduling build pod..."
                  NS=$(cat /var/run/secrets/kubernetes.io/serviceaccount/namespace)
                  echo "Using namespace: ${NS}"

                  ensure_pvc() {
                    local pvc_name="$1"
                    local manifest="$2"
                    if kubectl -n "${NS}" get pvc "${pvc_name}" >/dev/null 2>&1; then
                      echo "PVC ${pvc_name} already exists."
                    else
                      echo "PVC ${pvc_name} not found. Applying manifest ${manifest}..."
                      kubectl -n "${NS}" apply -f "${manifest}"
                      echo "Applied ${manifest}."
                    fi
                  }

                  ensure_pvc annotare-m2-cache app/annotare-m2-cache-pvc.yaml
                  ensure_pvc annotare-mysql-cache app/annotare-mysql-cache-pvc.yaml
                '''
            }
        }

        stage('Build on k8s') {
            agent {
                kubernetes {
                    cloud 'atlas-admin-2'
                    yamlFile 'app/annotare-maven-pod.yaml'
                    defaultContainer 'maven'
                }
            }
            stages {
                stage('Initialize') {
                    steps {
                        sh '''
                          echo "PATH = ${PATH}"
                          echo "M2_HOME = ${M2_HOME}"
                          echo "JAVA_HOME = ${JAVA_HOME}"
                        '''
                    }
                }

                stage('Build and Test') {
                    steps {
                        configFileProvider([configFile(fileId: 'annotare_wwwdev_maven_settings', variable: 'MAVEN_SETTINGS_XML')]) {
                            sh '''
                                echo "Waiting for MySQL sidecar to start..." && sleep 10
                                mvn -s $MAVEN_SETTINGS_XML -f app/pom.xml clean test -Dannotare.test.db.url=jdbc:mysql://127.0.0.1:3306/annotare -Dannotare.test.db.user=annotare -Dannotare.test.db.password=annotare -Dannotare.test.db.driver=com.mysql.cj.jdbc.Driver -Dmaven.wagon.http.ssl.insecure=true -Dmaven.wagon.http.ssl.allowall=true
                            '''
                        }
                    }
                }

                stage ('Build Artifacts') {
                    stages{
                        stage('Build') {
                            when { not { branch 'master'}}
                            steps{
                                configFileProvider([configFile(fileId: 'annotare_wwwdev_maven_settings', variable: 'MAVEN_DEV_SETTINGS_XML')]){
                                    sh 'mvn -s $MAVEN_DEV_SETTINGS_XML -f app/pom.xml package -DskipTests -Dannotare.test.db.url=jdbc:mysql://127.0.0.1:3306/annotare -Dannotare.test.db.user=annotare -Dannotare.test.db.password=annotare -Dannotare.test.db.driver=com.mysql.cj.jdbc.Driver -Dmaven.wagon.http.ssl.insecure=true -Dmaven.wagon.http.ssl.allowall=true'
                                }
                            }
                        }
                        stage('Master Build') {
                            when { branch 'master'}
                            steps{
                                configFileProvider([configFile(fileId: 'annotare_prod_maven_settings', variable: 'MAVEN_PROD_SETTINGS_XML')]) {
                                    sh 'mvn -s $MAVEN_PROD_SETTINGS_XML -f app/pom.xml package -DskipTests -Dannotare.test.db.url=jdbc:mysql://127.0.0.1:3306/annotare -Dannotare.test.db.user=annotare -Dannotare.test.db.password=annotare -Dannotare.test.db.driver=com.mysql.cj.jdbc.Driver -Dmaven.wagon.http.ssl.insecure=true -Dmaven.wagon.http.ssl.allowall=true'
                                }
                            }
                        }
                        stage('Archive artifacts') {
                            steps{
                                archiveArtifacts artifacts: 'app/webapp/target/annotare2-web*.war', fingerprint: true
                            }
                        }
                    }
                }

                stage ('Release') {
                    when { not { branch 'master'}}
                    steps{
                        release job: 'Annotare-web-app-release-wwwdev', parameters: [string(name: 'artifactBuildNumber', value: env.BUILD_NUMBER), string(name: 'branch_name', value: env.BRANCH_NAME)]
                    }
                }

                stage ('Production Release') {
                    when { branch 'master'}
                    steps{
                        release job: 'Annotare-web-app-release-prod', parameters: [string(name: 'artifactBuildNumber', value: env.BUILD_NUMBER), string(name: 'branch_name', value: env.BRANCH_NAME)]
                    }
                }
            }
        }
    }
    post {
        always {
            script {
                def buildStatus = currentBuild.result ?: 'SUCCESS'
                def githubStatus = (buildStatus == 'SUCCESS') ? 'SUCCESS' : 'FAILURE'
                githubNotify context: 'Jenkins',
                             description: "Build ${buildStatus.toLowerCase()}",
                             status: githubStatus
            }
        }
    }
}
